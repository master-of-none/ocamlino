module type Letter = sig
  type t

  val compare : t -> t -> int
end

module type PersistentSet = sig
  type elt

  type t

  val empty : t

  val mem : elt -> t -> bool

  val add : elt -> t -> t

  val remove : elt -> t -> t

  val inter : t -> t -> t

  val compare : t -> t -> int
end

module Make (L : Letter) : PersistentSet with type elt = L.t list = struct
  type elt = L.t list

  module M = Map.Make (L)

  type t =
    { word: bool
    ; branches: t M.t
    }

  let empty = { word = false; branches = M.empty }

  let is_empty t = (not t.word) && M.is_empty t.branches

  let rec mem x t =
    match x with
    | [] -> t.word
    | i :: l -> ( try mem l (M.find i t.branches) with Not_found -> false)

  let rec add x t =
    match x with
    | [] ->
        if t.word then
          t
        else
          { t with word = true }
    | i :: l ->
        let b = try M.find i t.branches with Not_found -> empty in
        { t with branches = M.add i (add l b) t.branches }

  let rec remove x t =
    match x with
    | [] -> { t with word = false }
    | i :: l -> (
        try
          let s = remove l (M.find i t.branches) in
          let new_branches =
            if is_empty s then
              M.remove i t.branches
            else
              M.add i s t.branches
          in
          { t with branches = new_branches }
        with Not_found -> t)

  let rec inter t1 t2 =
    { word = t1.word && t2.word
    ; branches = inter_branches t1.branches t2.branches
    }

  and inter_branches m1 m2 =
    M.fold
      (fun i ti m ->
        try
          let t = inter ti (M.find i m2) in
          if is_empty t then
            m
          else
            M.add i t m
        with Not_found -> m)
      m1 M.empty

  let rec compare t1 t2 =
    let c = Stdlib.compare t1.word t2.word in
    if c <> 0 then
      c
    else
      M.compare compare t1.branches t2.branches
end
